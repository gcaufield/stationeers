alias Dial d0
alias Button d1
alias FurnaceController d2
alias CountDial d3
alias RequestMem d4
alias Sorter d5

define LOOKUP       165
define ELEMENT_SIZE 11
define STATE_IDLE    0
define STATE_REQUEST 1
define STATE_WAIT    2
define STATE_ABORT   3

alias State r14
alias CurrentIngot r13
alias CurrentCount r12
alias RequestIter r11
alias RequestCount r10

s Dial Mode 15
move sp LOOKUP

brdns Sorter 2
s Sorter Mode 2
move State STATE_IDLE

start:
yield
beq State STATE_IDLE idle
beq State STATE_WAIT wait
beq State STATE_ABORT abort

idle:
jal CheckSorter
l r0 Dial Setting
brnez r0 3
s db Setting 0
j start
sub r0 r0 1
mul CurrentIngot r0 ELEMENT_SIZE
add r0 CurrentIngot 10
sub sp LOOKUP r0
pop r0
s db Setting r0
l r1 Button Activate
beqz r1 start
s FurnaceController Setting r0
l CurrentCount CountDial Setting
sdns r1 RequestMem
select State r1 STATE_WAIT STATE_REQUEST
beq State STATE_WAIT start # If we aren't making a request

move RequestCount 0
move RequestIter CurrentCount
loop1_start:
beqz RequestIter loop1_end
sub sp LOOKUP CurrentIngot
loop2_start:
pop r0
beqz r0 loop2_end
jal MakeRequest
beqz r0 endif1
s FurnaceController Setting 0
s Sorter ClearMemory 1
move State STATE_ABORT
j start
endif1:
add RequestCount RequestCount 1
j loop2_start
loop2_end:
sub RequestIter RequestIter 1
j loop1_start
loop1_end:
# We successfully requested everything open valve and wait.
move State STATE_WAIT
j start

wait:
jal CheckSorter
l r1 FurnaceController Setting
brnez r1 2
move State STATE_IDLE
j start

abort:
jal CheckSorter
l r1 Sorter ExportCount
brne r1 RequestCount 2 # Check if we have successfully returned everything we originally asked for
move State STATE_IDLE
j start

CheckSorter:
bdns Sorter ra # Noting to do if there is no sorter.
l r1 Sorter Output
bne r1 -1 ra # Sorter isn't ready 
ls r1 Sorter 0 Occupied
beqz r1 ra # Nothing in the sorter
seq r1 State STATE_WAIT # We only forward to the furnace in the Wait State.
s Sorter Output r1
j ra

MakeRequest:
l r1 RequestMem Setting
breqz r1 3
yield
jr -3
s RequestMem Setting r0
yield
l r1 RequestMem Setting # Wait for Ack from Warehouse
breq r0 r1 -2
sne r0 r1 -1 
s RequestMem Setting 0
j ra