alias Tank d0
alias PumpCtl d1
alias Furnace d2
alias CountDial d3
define LOOKUP       110
define ELEMENT_SIZE 11
alias MaxPress r14
alias MinPress r13
alias MaxTemp  r12
alias MinTemp  r11
alias RCount   r10
alias State    r9
alias LookupOffset r8
define STATE_IDLE 0
define STATE_LOADING 1
define STATE_RUNNING 2
define STATE_COMPLETE 3

s Furnace SettingInput 100
s Furnace SettingOutput 0

s db Setting 0
s PumpCtl Setting 0

move sp LOOKUP
move State STATE_IDLE

start:
yield
l r0 db Setting
beqz r0 idle
beq State STATE_LOADING load
beq State STATE_RUNNING run
beq State STATE_COMPLETE complete

move LookupOffset -1
move sp LOOKUP
beq sp 0 start
add LookupOffset LookupOffset 1
sub sp sp 10 # Remove all data
pop r1
brne r0 r1 -4
move State STATE_LOADING
mul LookupOffset LookupOffset ELEMENT_SIZE
sub sp LOOKUP LookupOffset
sub sp sp 5
pop RCount
l r1 CountDial Setting
mul RCount RCount r1
move MaxPress 1100
move MinPress 700
move MinTemp 600
move MaxTemp 100000

load:
jal manage_temp
l r0 Furnace Reagents
blt r0 RCount start # Still Waiting for Reagents
sub sp LOOKUP LookupOffset
sub sp sp 6
pop MaxPress
pop MinPress
pop MaxTemp
pop MinTemp
move State STATE_RUNNING
j start

run:
jal manage_temp

l r0 Furnace RecipeHash
l r1 db Setting
bne r0 r1 start
s Furnace Open 1
move State STATE_COMPLETE
j start

complete:
l r0 Furnace Reagents
brnez r0 4
s db Setting 0
s Furnace Open 0
move State STATE_IDLE
j start

idle:
move MaxPress 10000
move MinPress 700
move MinTemp 900
move MaxTemp 100000
jal manage_temp
j start

manage_temp:
l r0 Furnace Pressure
l r1 Furnace Temperature
l r2 Tank Temperature
brlt r0 MaxPress 5
slt r5 r1 1900 # Over Pressure
mul r5 r5 4
move r4 30
j mt_end
slt r6 r1 MinTemp # Under Temp Pump Fuel
sgt r4 r2 MinTemp # Can we get heat from tank?
select r5 r6 r4 0 # If we need heat and we can get it from the tank
select r6 r5 0 r6 # Pump fuel if the heat cant come from the tank.

slt r4 r0 MinPress # Under Pressure pump from tank
select r4 r4 r6 1 # Only pump from tank if we aren't trying to get heat from fuel
nor r4 r4 0
and r4 r4 1
or r5 r5 r4

sgt r7 r1 MaxTemp # Too hot pump coolant
select r5 r7 0 r5 # Only pump from the tank if we aren't pumping coolant.

mul r5 r5 1 # Tank
mul r6 r6 2 # Fuel
mul r7 r7 3 # Coolant
add r5 r5 r6
add r5 r5 r7
move r4 0
mt_end:
s PumpCtl Setting r5
s Furnace SettingOutput r4
j ra
