alias Engine d0
alias Sensor d1
alias PowerOutput d2
alias Switch d3

alias State r15
alias Stop r14

define Door HASH("StructureCompositeDoor")
define Vent HASH("StructureActiveVent")
define Regulator HASH("StructurePressureRegulator")
define Analyzer HASH("StructureCableAnalysizer")
define Valve HASH("StructureDigitalValve")

define DISABLED 0
define ENABLED 1
define PRIMING 2
define TargetPressure 150

define Outward 0
define Inward 1

move State DISABLED
sbn Vent HASH("Secondary") Lock 1
sbn Vent HASH("Secondary") Mode Inward 
sbn Vent HASH("Secondary") PressureExternal 0

start:
yield
s db Setting State
l r0 Engine PowerGeneration
lb r1 Analyzer PowerActual Maximum
sub r0 r0 r1
s PowerOutput Setting r0
move r1 2
brgtz r0 2
move r1 4
s PowerOutput Color r1

l Stop Switch Open
breqz Stop 2
move State DISABLED
beq State DISABLED disabled
beq State PRIMING prime

sb Valve On 1
s Engine On 1
l r0 Sensor Temperature
sub r0 r0 283
sub r1 150 r0
add r2 150 r0
sbn Vent HASH("Primary") Mode Inward
sbn Vent HASH("Primary") PressureExternal r1
sbn Vent HASH("Primary") On 1

sbn Vent HASH("Secondary") PressureInternal r2
sbn Vent HASH("Secondary") On 1

j start

disabled:
s Engine On 0
sb Valve On 0
sbn Vent HASH("Secondary") On 0
sbn Vent HASH("Primary") PressureExternal 0
sbn Vent HASH("Primary") Mode Inward

l r0 Sensor Pressure
snez r0 r0
sb Door Lock r0
sbn Vent HASH("Primary") On r0

bnez Stop start
sb Door Lock 1
sb Door Open 0
move State PRIMING
j start

prime:
sbn Vent HASH("Primary") Mode Outward
sbn Vent HASH("Primary") PressureExternal 160
sbn Vent HASH("Primary") On 1
l r0 Sensor Pressure
brgt r0 TargetPressure 4
sleep 1
l r1 Sensor Pressure
sub r1 r1 1 # Pressure Increase by atleast 1 kpa
blt r0 r1 start # If the pressure changed jump to start
move State ENABLED
j start
